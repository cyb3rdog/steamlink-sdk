#
# qmake configuration for the Marvell BG2CD
#
include(../common/linux_device_pre.conf)

EGLFS_PLATFORM_HOOKS_SOURCES = $$PWD/qeglfshooks_marvell.cpp

MARVELL_SDK            = $$(MARVELL_SDK_PATH)
MARVELL_APPROOT        = $$MARVELL_SDK/MRVL/MV88DE3100_SDK/Customization_Data/File_Systems/application_steam

QMAKE_LIBS_EGL         += -lEGL -lGAL -lVSC -lshm -lOSAL -lPEAgent
QMAKE_LIBS_OPENGL_ES2  += -lGLESv2 $$QMAKE_LIBS_EGL
QMAKE_LIBS_OPENVG      += -lOpenVG $$QMAKE_LIBS_EGL

QMAKE_LFLAGS           += -Wl,-rpath-link,$$[QT_SYSROOT]/lib -Wl,-rpath-link,$$[QT_SYSROOT]/usr/lib -Wl,-rpath-link,$$[QT_SYSROOT]/usr/lib/arm-linux-gnueabihf -L$$MARVELL_APPROOT/lib
QMAKE_LFLAGS           += --sysroot=$$[QT_SYSROOT]

MARVELL_CFLAGS         = -I$$MARVELL_APPROOT/include -DLINUX -DEGL_API_FB
QMAKE_CFLAGS           += $$MARVELL_CFLAGS
QMAKE_CXXFLAGS         += $$MARVELL_CFLAGS
DISTRO_OPTS            += hard-float

include(../common/linux_arm_device_post.conf)

load(qt_config)
